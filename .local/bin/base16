#!/usr/bin/env zsh

# Main function to set the shell theme and generate the Lua file
main() {
  if [[ $# -ne 1 ]]; then
    echo "Usage: $0 <theme>"
    return 1
  fi

  local theme="$1"
  local theme_script="$BASE16_SHELL/scripts/base16-$theme.sh"

  if [[ -f "$theme_script" ]]; then
    # Set the shell theme
    source "$theme_script"

    # Generate the Lua file for WezTerm and Neovim
    local wezterm_theme_name="$(echo "$theme" | gsed 's/-/ /g; s/\b\(.\)/\u\1/g') (base16)"
    local neovim_theme_name="base16-$theme"
    echo "return { wezterm = \"${wezterm_theme_name}\", neovim = \"${neovim_theme_name}\" }" > "$HOME/.local/share/base16-theme.lua"
    ln -fs $theme_script $HOME/.base16_theme
    # In order for this to all work with wezterm, you must also press
    # ctrl-shift-r to reload the config
  else
    echo "Theme '$theme' not found."
    return 1
  fi
}

# Call the main function with the provided argument
if [[ $ZSH_EVAL_CONTEXT == 'toplevel' ]]; then
  source "$HOME/.zshrc"
  main "$@"
else
  # Custom completion function
  _base16_custom_completion() {
    local completions
    completions=($(find "$BASE16_SHELL/scripts" -type f -name "*.sh" | sed -e 's/.*base16-//' -e 's/\.sh//' | sort))
    _describe 'values' completions
  }

  # Register the custom completion function for the script
  compdef _base16_custom_completion base16
fi
